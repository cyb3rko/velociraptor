buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.6'
    }
}
plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-kapt'

android {
    compileSdk 34
    packagingOptions {
        jniLibs {
            excludes += ['kotlin/**']
        }
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE', 'kotlin/**', '**/*.kotlin_metadata', 'META-INF/*.kotlin_module', 'META-INF/*.version']
        }
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_VELOCIRAPTOR
                keyPassword RELEASE_KEY_PASSWORD_VELOCIRAPTOR
            }
        }
    }


    namespace "com.pluscubed.velociraptor"

    defaultConfig {
        applicationId "com.pluscubed.velociraptor"
        minSdk 23
        targetSdk 34
        versionCode 60
        versionName "2.0.0"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [appName: "@string/app_name"]

        lintOptions {
            disable 'MissingTranslation'
            disable 'ExtraTranslation'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debugIap {
            debuggable true
            multiDexEnabled true

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debug {
            multiDexEnabled true

            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            testCoverageEnabled false
            manifestPlaceholders = [appName: "Velociraptor DEBUG"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        viewBinding true
    }
}

ext{
    room_version = '2.2.5'
    ktor_version = '2.3.5'
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.robolectric:robolectric:4.4'

    //debugImplementation 'im.dino:dbinspector:3.4.1@aar'
    //debugIapImplementation 'im.dino:dbinspector:3.4.1@aar'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.google.android.material:material:1.10.0'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation "androidx.lifecycle:lifecycle-service:2.2.0"

    implementation "com.google.android.gms:play-services-location:17.1.0"
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation "com.google.android.gms:play-services-maps:17.0.0"
    implementation "com.google.maps.android:android-maps-utils:2.2.0"

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"

    implementation 'com.github.bumptech.glide:glide:3.8.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.github.cyb3rko:quickpermissions-kotlin:1.1.2'

    // LeakCanary temporarily disabled
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
}

configurations {
    configureEach {
        exclude group: 'com.android.support', module: 'support-v4'
    }
}
